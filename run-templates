#!/usr/bin/env perl

use strict;
use warnings;
use autodie;
use 5.020;
use experimental qw( postderef );
use Template;
use File::chdir;
use Path::Tiny qw( path );

my $tt = Template->new;
my $stable        = 'buster';
my $latest_stable = '5.32.0';
my $latest_dev    = '5.31.11';

my %perl_sources = (
  '5.28.1'  => 'https://cpan.metacpan.org/authors/id/S/SH/SHAY/perl-5.28.1.tar.gz',
  '5.28.2'  => 'https://cpan.metacpan.org/authors/id/S/SH/SHAY/perl-5.28.2.tar.gz',
  '5.30.0'  => 'https://cpan.metacpan.org/authors/id/X/XS/XSAWYERX/perl-5.30.0.tar.gz',
  '5.30.1'  => 'https://cpan.metacpan.org/authors/id/S/SH/SHAY/perl-5.30.1.tar.gz',
  '5.30.2'  => 'https://cpan.metacpan.org/authors/id/S/SH/SHAY/perl-5.30.2.tar.gz',
  '5.30.3'  => 'https://cpan.metacpan.org/authors/id/X/XS/XSAWYERX/perl-5.30.3.tar.gz',
  '5.31.1'  => 'https://cpan.metacpan.org/authors/id/E/ET/ETHER/perl-5.31.1.tar.gz',
  '5.31.2'  => 'https://cpan.metacpan.org/authors/id/S/SH/SHAY/perl-5.31.2.tar.gz',
  '5.31.6'  => 'https://cpan.metacpan.org/authors/id/B/BI/BINGOS/perl-5.31.6.tar.gz',
  '5.31.7'  => 'https://cpan.metacpan.org/authors/id/A/AT/ATOOMIC/perl-5.31.7.tar.gz',
  '5.31.8'  => 'https://cpan.metacpan.org/authors/id/W/WO/WOLFSAGE/perl-5.31.8.tar.gz',
  '5.31.9'  => 'https://cpan.metacpan.org/authors/id/R/RE/RENEEB/perl-5.31.9.tar.gz',
  '5.31.10' => 'https://cpan.metacpan.org/authors/id/X/XS/XSAWYERX/perl-5.31.10.tar.gz',
  '5.31.11' => 'https://cpan.metacpan.org/authors/id/X/XS/XSAWYERX/perl-5.31.11.tar.gz',
);

# build the Dockerfiles for the base images
{
  my $arch = 'x86_64';
  my %deb = (
    'i386/debian'    => [ qw( buster32 bullseye32 ) ],
    'arm64v8/debian' => [ qw( buster-arm64v8 ) ],
    'debian'         => [ qw( stretch buster bullseye ) ],
    'ubuntu'         => [ qw( xenial bionic ) ],
  );
  my %extra = (
  );

  foreach my $dist (sort keys %deb)
  {
    my $arch = $dist =~ /^arm64v8\// ? 'aarch64' : 'x86_64';
    foreach my $dist_release (sort $deb{$dist}->@*)
    {
      my $path = path("base/$arch")->child($dist_release);
      $path->mkpath;
      $path = $path->child('Dockerfile');
      $tt->process(
        "rc/Dockerfile-base.tt",
        { dist => $dist, dist_release => $dist_release =~ s/(32|-arm64v8)$//r, %{ $extra{$dist_release} // {} } },
        "$path"
      ) || die $tt->error
    }
  }

  my %fedora = (
    fedora => [ 29 ],
    centos => [ 6, 7 ],
  );

  foreach my $dist (sort keys %fedora)
  {
    foreach my $dist_version (sort $fedora{$dist}->@*)
    {
      my $dist_release = "$dist$dist_version";
      my $path = path("base/$arch")->child($dist_release);
      $path->mkpath;
      $path = $path->child('Dockerfile');
      $tt->process(
        "rc/Dockerfile-base-fedora.tt",
        { dist => $dist, dist_version => $dist_version, %{ $extra{$dist_release} // {} } },
        "$path"
      ) || die $tt->error
    }
  }

  my %alpine = (
    alpine => [ '3.9' ],
  );
  foreach my $dist (sort keys %alpine)
  {
    foreach my $dist_version (sort $alpine{$dist}->@*)
    {
      my $dist_release = "$dist$dist_version";
      my $path = path("base/$arch")->child($dist_release);
      $path->mkpath;
      $path = $path->child('Dockerfile');
      $tt->process(
        "rc/Dockerfile-base-alpine.tt",
        { dist => $dist, dist_version => $dist_version, %{ $extra{$dist_release} // {} } },
        "$path"
      ) || die $tt->error
    }
  }
}

sub generate_dockerfile
{
  my($tag, $aliases, %vars) = @_;
  my $arch = $vars{dist_release} =~ /-arm64v8/ ? 'aarch64' : 'x86_64';
  my $path = path("versions/$arch")->child($tag);
  $path->mkpath;
  foreach my $alias ($aliases->@*)
  {
    my $alias_path = path("versions/$arch")->child($alias);
    if(-e $alias_path)
    {
      if(! -l $alias_path)
      {
        use Carp qw( croak );
        croak "alias path $alias_path is not a symlink!";
      }
      unlink $alias_path;
    }
    symlink $path->basename, "$alias_path";
  }

  my $fh;
  open $fh, "|cat -s > @{[ $path->child('Dockerfile') ]}";
  
  $tt->process(
    "rc/Dockerfile.tt",
    \%vars,
    $fh,
  ) || die $tt->error;

  close $fh;
}

# build recent versions of bullseye for now
foreach my $perl_version ($latest_stable, qw( 5.30.3 5.28.3 5.26.3 5.24.4 5.22.4 5.20.3 ))
{
  foreach my $dist_release ('bullseye')
  {
    my $tag = "$perl_version-$dist_release";
    my @aliases;
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$dist_release";
    generate_dockerfile($tag, \@aliases,
      no_perl      => 0,
      dist_release => $dist_release,
      perl_version => $perl_version,
      perl_source  => $perl_sources{$perl_version},
    );
  }
}

foreach my $perl_version ($latest_stable, qw( 5.30.3 5.28.2 5.26.3 5.24.4 5.22.4 5.20.3 5.18.4 5.16.3 5.14.4 ))
{
  my @aliases;
  push @aliases, $perl_version;
  push @aliases, $perl_version =~ s/\.[0-9]+$//r;
  push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$stable";
  generate_dockerfile("$perl_version-$stable", \@aliases,
    dist_release => $stable,
    perl_version => $perl_version,
  );
}

foreach my $perl_version ($latest_dev)
{
  my @aliases;
  push @aliases, $perl_version;
  push @aliases, $perl_version =~ s/\.[0-9]+$//r;
  my $rc = 0;
  if($perl_version =~ /-RC[0-9]/)
  {
    $rc = 1;
    push @aliases, $perl_version =~ s/\.[0-9]+-RC[0-9]+$//r;
    push @aliases, $perl_version =~ s/-RC[0-9]+$//r . "-$stable";
    push @aliases, $perl_version =~ s/\.[0-9]+-RC[0-9]+$//r . "-$stable";
  }
  generate_dockerfile("$perl_version-$stable", \@aliases,
    dist_release => $stable,
    perl_dev     => 1,
    perl_suffix  => $perl_version =~ s/(-RC[0-9]+|)$//r,
    perl_flags   => '-Dusedevel',
    perl_version => $perl_version,
  );
}

foreach my $perl_version (qw( 5.12.5 5.10.1 5.10.0 5.8.9 5.8.8 5.8.7 5.8.6 5.8.5 5.8.4 ))
{
  my @aliases;
  push @aliases, $perl_version;
  if($perl_version !~ /^5.8/ || $perl_version eq '5.8.8')
  {
    push @aliases, $perl_version =~ s/\.[0-9]+$//r;
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$stable";
  }
  generate_dockerfile("$perl_version-$stable", \@aliases,
    old          => '5.24.4',
    dist_release => $stable,
    perl_version => $perl_version,
  );
}

foreach my $flag (qw( threads longdouble quadmath ))
{
  foreach my $perl_version ('5.8.4', '5.8.8', '5.10.0', '5.10.1', '5.14.4', $latest_stable)
  {
    next if $flag ne 'threads' && $perl_version =~ /^(5\.8|5\.10)\./;
    next if $flag eq 'quadmath' && $perl_version eq '5.14.4';
    my $old;
    $old = '5.24.4' if $perl_version =~ /^(5\.8|5\.10)\./;
    my $tag = "$perl_version-$stable-$flag";
    my @aliases;
    push @aliases, "$perl_version-$flag";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$flag";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$stable-$flag";
    generate_dockerfile($tag, \@aliases,
      old          => $old,
      perl_flags   => "-Duse$flag",
      dist_release => $stable,
      perl_version => $perl_version,
    );
  }
}

foreach my $perl_version ($latest_stable)
{

  foreach my $dist_release (qw( bionic buster bullseye buster-arm64v8 buster32 bullseye32 jessie xenial disco centos6 centos7 fedora29  ))
  {
    my $tag = "$perl_version-$dist_release";
    my @aliases;
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$dist_release";
    my $no_perl = 0;
    $no_perl = 1 if $dist_release =~ /^(fedora|centos)/;
    generate_dockerfile($tag, \@aliases,
      no_perl      => $no_perl,
      dist_release => $dist_release,
      perl_version => $perl_version,
      perl_source  => $perl_sources{$perl_version},
    );
  }

  foreach my $dist_release (qw( alpine3.9 ))
  {
    my $tag = "$perl_version-$dist_release-threads";
    my @aliases;
    push @aliases, "$perl_version-$dist_release";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$dist_release-threads";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$dist_release";
    my $no_perl = 0;
    $no_perl = 1 if $dist_release =~ /^(alpine)/;
    generate_dockerfile($tag, \@aliases,
      no_perl      => $no_perl,
      dist_release => $dist_release,
      perl_version => $perl_version,
      perl_source  => $perl_sources{$perl_version},
      perl_flags   => "-Dusethreads",
    );
  }

  # libffi master
  {
    my $tag = "$perl_version-$stable-ffimaster";
    my @aliases;
    push @aliases, "$perl_version-ffimaster";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$stable-ffimaster";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-ffimaster";
    generate_dockerfile($tag, \@aliases,
      dist_release => $stable,
      perl_version => $perl_version,
      ffimaster    => 1,
    );
  }

  # memory debug
  {
    my $tag = "$perl_version-$stable-debug";
    my @aliases;
    push @aliases, "$perl_version-debug";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-$stable-debug";
    push @aliases, ($perl_version =~ s/\.[0-9]+$//r) . "-debug";
    generate_dockerfile($tag, \@aliases,
      dist_release => $stable,
      perl_version => $perl_version,
      perl_flags   => '-DDEBUGGING -Doptimize=-g3 -Accflags="-DDEBUG_LEAKING_SCALARS"',
      packages => {
        debian => 'valgrind',
      },
      ffimaster    => 1,
    );
  }
}

# default
{
  unlink 'versions/x86_64/default' if -l 'versions/x86_64/default';
  symlink "$latest_stable", 'versions/x86_64/default';
}

# static
{
  mkdir 'versions/x86_64/static' unless -d 'versions/x86_64/static';
  path('versions/x86_64/static/Dockerfile')->spew(
    path('rc/Dockerfile.static')->slurp
  );
}
